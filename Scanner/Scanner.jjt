options {
    MULTI=true;
    VISITOR=true;
    NODE_PREFIX="";
    VISITOR_DATA_TYPE="SimpleNode";
    VISITOR_RETURN_TYPE="SimpleNode";
}

PARSER_BEGIN(Scanner)

import java.io.*;

public class Scanner {

    public static void main(String[] args) throws ParseException, FileNotFoundException {
        File file = new File("test.txt");
        FileInputStream stream = new FileInputStream(file);
        Scanner parser = new Scanner(stream);

        try {
            SimpleNode n = parser.PROG();
            //n.dump("");

            /*System.out.println("Pretty printing:");
            PrettyPrinterVisitor ppv = new PrettyPrinterVisitor();
            n.jjtAccept(ppv, null);
            System.out.println();*/

            System.out.println("Filling the symboltable:");
            SymbolTableVisitor stv = new SymbolTableVisitor();
            n.jjtAccept(stv, null);
            System.out.println(SymbolTableVisitor.ST);
            System.out.println("Finished succesfully");
        }catch(Exception e) {
            System.out.println(e.getMessage());
            e.printStackTrace();
        }
    }
}

PARSER_END(Scanner)

SKIP: /* Whitespace */
{
  "\t"
| "\r"
| " "
}

TOKEN: /* Keywords */
{
    <IMPORT:        "IMPORT">
|   <IN:            "IN">
|   <MODEL:         "MODEL">
|   <ANALYZE:       "ANALYZE">
|   <WITH:          "WITH">
|   <ROW:           "ROW">
|   <COL:           "COL">
|   <NOHEADERS:     "NOHEADERS">
|   <IDKEY:         "ID">
|   <TO:            "TO">
|   <WHERE:         "WHERE">
}

TOKEN: /* Whitespace */
{
    <EOL:           "\n">
}

TOKEN: /* Operators */
{
    <COMPARISON:    "<=" | ">=" | "<" | ">">
|   <EQUAL:         "=">
|   <DEFINERULE:    ":" >
|   <IS:            "IS" >
|   <OR:            "OR">
|   <AND:           "AND">
|   <NOT:           "NOT">
|   <SUM:           "SUM">
|   <AVG:           "AVG">
|   <COUNT:         "COUNT">
|   <DISTINCT:      "DISTINCT">
}

TOKEN: /* Regex */
{
    <CONSTRAINT:    "LETTERS"|"DECIMAL"|"INTEGER">
|   <EMPTY:         "EMPTY">
|   <CONTAINS:      "CONTAINS">
}

TOKEN: /* Delimiters */
{
    <COMMA:      ",">
|   <LPAREN:     "(">
|   <RPAREN:     ")">
|   <ARGSSTART:  "BEGIN">
|   <ARGSSTOP:   "END">
}

TOKEN: /* Literals */
{
    <INT:           ("-")?["1"-"9"](["0"-"9"])* | ("-")?"0" >
|   <FLOAT:         ("-")?"0" "." (["0"-"9"])+ | ("-")?["1"-"9"] (["0"-"9"])* "." (["0"-"9"])+ >
|   <STRING:        "\"" (~["\""])* "\"" >
}

TOKEN: /* Identifiers */
{
    <FILENAME:      (["A"-"Z","a"-"z","0"-"9","-","_"])+".csv">
|   <ID:            (["A"-"Z","a"-"z"](["A"-"Z","a"-"z","0"-"9","_"])*)|("_"(["A"-"Z","a"-"z","0"-"9"])(["A"-"Z","a"-"z","0"-"9","_"])*)>
}


MORE :
{
    "//" : IN_COMMENT
}

<IN_COMMENT>
SPECIAL_TOKEN :
{
  <COMMENT: "//" > : DEFAULT
}

<IN_COMMENT>
MORE :
{
  < ~[] >
}

SimpleNode PROG():    {}  
{ 
    (MDL() IMPRT() STMT() | IMPRT() MDL() STMT()) 
    { return jjtThis; }
}

void IMPRT() #IMPORT:   
{}  
{ <IMPORT> FLNM() <IN> IDEN() (<ARGSSTART> (<EOL>)+ IMOPTIONS() <ARGSSTOP>)? (<EOL>)+ }

void IMOPTIONS() #IMPOPTIONS:
{}
{
    NOHEADERS() (<EOL>)+ (<IDKEY> (IDEN() | INTEGER()) (<EOL>)+)? 
|   <IDKEY> (IDEN() | INTEGER()) (<EOL>)+ (NOHEADERS() (<EOL>)+)?
}

void FLNM():    
{ Token t; }
{
    t = <FILENAME>
    {
        jjtThis.value = t.image;
    }
}

void NOHEADERS() #NOHEADERS:
{}
{
    <NOHEADERS>
}

void MDL() #MODEL:     
{}  
{ <MODEL> IDEN() <ARGSSTART> (<EOL>)+ (RLE() | COLRLE())* <ARGSSTOP> (<EOL>)+}

void COLRLE() #void:
{}
{ 
    ( <COL> IDEN() (
        <DEFINERULE> COLEXPR() (WHERE())? (<EOL>)+
    |   <ARGSSTART> (<EOL>)+ (COLPARTRLE())+ <ARGSSTOP> (<EOL>)+
    )
    )#COLRULE(>1) 
}

void COLPARTRLE() #COLPARTRULE:
{}
{
    IDEN() <DEFINERULE> COLEXPR() (WHERE())? (<EOL>)+
}

void COLEXPR() #void:
{}
{
   COLOREXPR()
}

void COLOREXPR() #void:
{ int count = 0; }
{
    (COLANDEXPR() (<OR> { count++; } COLANDEXPR())*)#OR(count>0)
}

void COLANDEXPR() #void:
{ int count = 0; }
{
    (COLVPEXPR() (<AND> { count++; } COLVPEXPR())*)#AND(count>0)
}

void COLVPEXPR() #void:
{}
{
    COLVALEXPR()
    | <LPAREN> COLEXPR() <RPAREN>
}

void COLVALEXPR():
{ Token t; }
{
    SACD() (t = <EQUAL> { jjtThis.value = t.image;} | t = <COMPARISON> { jjtThis.value = t.image;}) COLARITHMEXPR()
}

void WHERE():
{}
{
    <WHERE> EXPR()
}

void SACD():
{ Token t; }
{
    (t = <SUM> | t = <AVG> | t = <COUNT> | t = <DISTINCT>) <LPAREN> IDEN() <RPAREN>
    { jjtThis.value = t.image; }
}

void COLARITHMEXPR() #void:
{ Token t; }
{
    (
        COLARITHMEXPR2() (( t = "+" | t = "-") { jjtThis.value = t.image; } COLARITHMEXPR2())*
    )#ADD(>1)
}

void COLARITHMEXPR2() #void:
{ Token t; }
{
    (
        COLOPERANDS()((t = "*" | t = "/" | t = "%") { jjtThis.value = t.image; } COLOPERANDS())*
    )#MULT(>1)
}

void COLOPERANDS() #void:
{}
{
    <LPAREN> COLARITHMEXPR() <RPAREN> | SACD() | NUM()
}

void RLE() #void:
{}
{ 
    (IDEN() (
        <DEFINERULE> EXPR() (<EOL>)+
    |   <ARGSSTART> (<EOL>)+ (PARTRLE())+ <ARGSSTOP> (<EOL>)+
    )
    )#RULE(>1) 
}

void PARTRLE() #PARTRULE:
{}
{
    IDEN() <DEFINERULE> EXPR() (<EOL>)+
}

void EXPR() #void:
{}
{
    OREXPR()
}

void OREXPR() #void:
{ int count = 0; }
{
    (ANDEXPR() (<OR> { count++; } ANDEXPR())*)#OR(count>0)
}

void ANDEXPR() #void:
{ int count = 0; }
{
    (VPEXPR() (<AND> { count++; } VPEXPR())*)#AND(count>0)
}

void VPEXPR() #void:
{}
{
    VALEXPR()
    | <LPAREN> EXPR() <RPAREN>
}

void VALEXPR():
{ Token t; }
{
    IDEN()
    (
        t = <IS> CONSTRAINEXPR()
    |   t = <EQUAL> (STRING() | ARITHMEXPR1())
    |   t = <COMPARISON> ARITHMEXPR1()
    |   t = <CONTAINS> STRING()
    ) 
    { jjtThis.value = t.image;}
}

void CONSTRAINEXPR() #void:
{}
{
    CONSTRAINTS() 
}

void ARITHMEXPR1() #void:
{ Token t; }
{
    (
        ARITHMEXPR2() (( t = "+" | t = "-") { jjtThis.value = t.image; } ARITHMEXPR2())*
    )#ADD(>1)
}

void ARITHMEXPR2() #void:
{ Token t; }
{
    (
        OPERANDS()((t = "*" | t = "/" | t = "%") { jjtThis.value = t.image; } OPERANDS())*
    )#MULT(>1)
}

void CONSTRAINTS():
{ Token t; }
{
    t = <CONSTRAINT>
    { jjtThis.value = t.image; } |
    t = <EMPTY> { jjtThis.value = "EMPTY"; } |
    t = <NOT> <EMPTY> { jjtThis.value = "NOTEMPTY"; }
}

void OPERANDS() #void:
{}
{
    <LPAREN> ARITHMEXPR1() <RPAREN> | (SACD()) | IDEN() | NUM()
}

void STRING():
{ Token t; }
{
    (t = <STRING> { jjtThis.value = t.image; })
}

void NUM() #void:     
{}
{ INTEGER() | FLOATY() }

void INTEGER(): 
{ Token t; }
{
    t = <INT>
    {
        jjtThis.value = t.image;
    }
}

void FLOATY(): 
{ Token t; }
{
    t = <FLOAT>
    {
        jjtThis.value = t.image;
    }
}

void STMT() #void:
{}
{ ANLZ() ((<EOL>)+ | <EOF>) STMT() | <EOF> }

void ANLZ() #ANALYZE:
{}
{ <ANALYZE> IDEN() <WITH> IDEN() (<ARGSSTART> (<EOL>)+ ANOPTIONS() <ARGSSTOP>)?}

void ANOPTIONS() #ANLZOPTIONS:
{}
{
    RULEOPT() (ROWOPT() (COLOPT())? | COLOPT() (ROWOPT())?)
}

void RULEOPT():
{}
{
    (IDEN() (<EOL>)+)*
}

void ROWOPT() #void:
{}
{
   ROWS() (<EOL>)+
}

void COLOPT() #void:
{}
{
    COLS() (<EOL>)+
}

void ROWS():
{}
{
    <ROW> INDEX()(<COMMA> INDEX())*
}

void COLS():
{}
{
    <COL> ( IDEN()(<COMMA> IDEN())* | (INDEX()(<COMMA> INDEX())*))
}

void INDEX() #void:
{ int count = 0; }
{
    (INTEGER() (<TO> {count++;} INTEGER())?)#RANGE(count>0)
}

void IDEN():
{ Token t; }
{
    t = <ID>
    {
        jjtThis.value = t.image;
    }
}